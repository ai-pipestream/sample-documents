pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

rootProject.name = 'sample-documents'

dependencyResolutionManagement {
    repositories {
        mavenLocal {
            mavenContent {
                releasesOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        mavenCentral()

        // Maven Central snapshots host ai.pipestream SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Access Apache snapshots only for select dependencies when not on internal CI
        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                mavenContent {
                    snapshotsOnly()
                }
            }
        }
    }
}

// Include all subprojects - BOM first so it's available for other projects
include 'bom'

// Test data modules
include 'sample-doc-types'
include 'test-documents'